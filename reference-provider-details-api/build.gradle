plugins {
    id 'org.openapi.generator' version '7.14.0'
    id 'uk.gov.laa.springboot.laa-spring-boot-gradle-plugin' version '1.0.2'
}

repositories {
    mavenCentral()
}

dependencyManagement {
    dependencies {
        // Force dependency versions to fix vulnerabilities
        dependencySet(group: 'ch.qos.logback', version: '1.5.19') {
            entry 'logback-classic'
            entry 'logback-core'
        }
        dependencySet(group: 'org.apache.tomcat.embed', version: '11.0.9') {
            entry 'tomcat-embed-core'
            entry 'tomcat-embed-websocket'
        }
        dependency 'org.yaml:snakeyaml:2.0'
        dependency 'org.apache.commons:commons-lang3:3.18.0'
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
}

sourceSets.main.java.srcDirs += ['generated/src/main/java']

checkstyleMain.exclude "*"

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/reference-provider-details-api/open-api-specification.json".toString()
    outputDir = "$rootDir/reference-provider-details-api/generated".toString()
    apiPackage = "uk.gov.justice.laa.provider.api"
    invokerPackage = "uk.gov.justice.laa.provider.invoker"
    modelPackage = "uk.gov.justice.laa.provider.model"
    configOptions = [
            generateBuilders    : "true",
            interfaceOnly       : "true", // This will only generate interfaces, not implementations
            serializableModel   : "true",
            skipDefaultInterface: "true",
            useJakartaEe        : "true",
            useSpringBoot3      : "true",
            useTags             : "true",
    ]
}

compileJava.dependsOn 'openApiGenerate'

clean {
    delete "$rootDir/reference-provider-details-api/generated"
}

// disable for overall project when running a build
tasks.named('bootJar') {
    enabled = false
}

tasks.named('bootRun') {
    enabled = false
}
tasks.withType(Test).configureEach {
    useJUnitPlatform()
}