plugins {
    id 'io.freefair.lombok' version '8.14'
    id("com.diffplug.spotless") version "7.2.1"
    id 'uk.gov.laa.springboot.laa-spring-boot-gradle-plugin' version '2.0.8'
    id "io.sentry.jvm.gradle" version "5.12.2"
    id 'au.com.dius.pact' version '4.6.19'
}

def versions = [
        sentry : '8.31.0',
        testcontainers : '1.20.1'
]

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/DataClaimsEventServiceApplication.class',
                                        '**/controller/**','**/service/TempService.class'
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/DataClaimsEventServiceApplication.class',
                                        '**/controller/**','**/service/TempService.class'])
        }))
    }
}

dependencyManagement {
    imports {
        // Upgrade to fix vulnerabilities in netty
        mavenBom('io.netty:netty-bom:4.2.9.Final')
    }
    dependencies {
        dependencySet(group: 'io.sentry', version: versions.sentry) {
            entry 'sentry-spring-boot-starter'
            entry 'sentry-logback'
        }
    }
}

dependencies {

    /**
     * Upgrades to resolve vulnerabilities
     */

    // Upgrade to fix CVE-2026-1225 vulnerability in logback-core
    implementation 'ch.qos.logback:logback-classic:1.5.25'

    implementation project(':reference-provider-details-api')
    implementation project(':reference-fee-scheme-platform-api')

    implementation 'uk.gov.justice.laa.datastewardship.payments:data-claims-api-models:6ec4cb7'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'io.prometheus:prometheus-metrics-tracer-initializer'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    implementation 'org.springframework.data:spring-data-commons'

    // Import spring-cloud-aws BOM (Bill Of Materials) for dependency management
    implementation platform('io.awspring.cloud:spring-cloud-aws:4.0.0')
    implementation 'io.awspring.cloud:spring-cloud-aws-starter'
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-sqs'
    implementation 'software.amazon.awssdk:sts:2.33.7'

    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    implementation 'jakarta.validation:jakarta.validation-api:3.1.1'
    implementation 'org.projectlombok:lombok:1.18.38'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'

    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    implementation platform('com.fasterxml.jackson:jackson-bom:2.19.2')
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'com.networknt:json-schema-validator:1.5.8'
    implementation 'io.github.resilience4j:resilience4j-spring-boot3:2.1.0'
    implementation 'io.github.resilience4j:resilience4j-reactor:2.1.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testImplementation "org.testcontainers:junit-jupiter:$versions.testcontainers"
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation "org.testcontainers:mockserver:$versions.testcontainers"
    testImplementation "org.testcontainers:localstack:$versions.testcontainers"
    testImplementation 'org.awaitility:awaitility'
    testImplementation 'org.mock-server:mockserver-client-java:5.15.0'
    testImplementation("io.projectreactor:reactor-test:3.1.0.RELEASE")
    // Sentry
    implementation platform("io.sentry:sentry-bom:$versions.sentry")
    implementation "io.sentry:sentry-logback:$versions.sentry"
    implementation "io.sentry:sentry-spring-boot-4:$versions.sentry"

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'au.com.dius.pact.consumer:junit5:4.6.19'
}

// To keep pact stuff separate
sourceSets {
    pactTest {
        java.srcDir file('src/pactTest/java')
        resources.srcDir file('src/pactTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
    }
}

configurations {
    pactTestImplementation.extendsFrom testImplementation
    pactTestRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.register('pactTest', Test) {
    systemProperty "pact.content_type.override.application/json", "application/json;charset=utf-8"

    description = 'Runs the pact tests.'
    group = 'verification'

    def pactsDir = layout.buildDirectory.dir("pacts").get().asFile
    doFirst {
        if (pactsDir.exists()) {
            pactsDir.deleteDir()
        }
    }

    testClassesDirs = sourceSets.pactTest.output.classesDirs
    classpath = sourceSets.pactTest.runtimeClasspath
    useJUnitPlatform()
    // Resolve the directory path to a String for the system property
    systemProperty 'pact.rootDir', layout.buildDirectory.dir("pacts").get().asFile.path
}

tasks.named('processPactTestResources') {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

spotless {
    java {
        target 'src/*/java/**/*.java'
        importOrder()
        removeUnusedImports()
        cleanthat()
        googleJavaFormat('1.27.0')
    }
}

tasks.register("checkstyleStaged", Checkstyle) {
    group = "verification"
    description = "Runs Checkstyle on staged files only"

    def filesToCheck = project.findProperty("checkstyleFiles")
    if (filesToCheck != null) {
        source = files(filesToCheck.split(","))
    } else {
        source = sourceSets.main.allJava
    }

    classpath = sourceSets.main.compileClasspath
}

pact {
    publish {
        pactBrokerUrl = project.findProperty('pactBrokerUrl') ?: System.getenv('PACT_BROKER_URL') ?: 'https://laa-data-pact-broker.apps.live.cloud-platform.service.justice.gov.uk/'
        pactBrokerUsername = project.findProperty('pactBrokerUsername') ?: System.getenv('PACT_BROKER_USERNAME') ?: 'ABC'
        pactBrokerPassword = project.findProperty('pactBrokerPassword') ?: System.getenv('PACT_BROKER_PASSWORD') ?: 'ABC'

        pactDirectory = layout.buildDirectory.dir("pacts").get().asFile.path

        def projectVersion = project.version != 'unspecified' ? project.version : '1.0.0';
        def gitSha = providers.environmentVariable('GIT_SHA').getOrNull()

        // Define the branch from a property, environment variable, or fallback to 'main'
        def branchName = providers.environmentVariable('GIT_BRANCH').getOrElse('main')

        // If we have a SHA, append it to the version for uniqueness
        consumerVersion = gitSha ? gitSha : projectVersion

        // For newer Pact Brokers (Recommended)
        consumerBranch = branchName
        // For older Brokers/compatibility
        tags = [branchName, projectVersion]

        // Optional: add a generic 'dev' tag for anything not on main
        if (branchName != 'main' && branchName != 'master') {
            tags.add('dev')
        }

    }
}


testlogger {
    theme 'mocha'
}
